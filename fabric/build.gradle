plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

file("../gradle.properties").withInputStream {
    def props = new Properties()
    props.load(it)
    props.each { prop -> project.ext.set(prop.key, prop.value) }
}

version = mod_version
group = mod_group

base {
    archivesName = "${mod_name}-${minecraft_version}-fabric"
}

eclipse {
    project { name = "${mod_name}-${minecraft_version}-fabric" }
}

sourceSets.main.java { srcDir '../src/main/java' }
sourceSets.main.resources { srcDir '../src/main/resources' }

loom {
	accessWidenerPath = file("src/main/resources/musketmod.aw")

    runs {
        client {
            runDir = '../run'
            programArgs.add('--username')
            programArgs.add('Dev')
        }
        server {
            runDir = '../run'
        }
    }
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

processResources {
    inputs.property "version", mod_version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
