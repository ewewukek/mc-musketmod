plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
}

file("../gradle.properties").withInputStream {
    def props = new Properties()
    props.load(it)
    props.each { prop -> project.ext.set(prop.key, prop.value) }
}

version = mod_version
group = maven_group
archivesBaseName = "${mod_name}-${minecraft_version}-fabric"

eclipse {
    project { name = "${mod_name}-fabric" }
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

sourceSets.main.java { srcDir '../src/main/java' }
sourceSets.main.resources { srcDir '../src/main/resources' }

loom {
    runs {
        client {
            runDir = '../run'
            programArgs.add('--username')
            programArgs.add('Dev')
        }
        server {
            runDir = '../run'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

processResources {
    inputs.property "version", mod_version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    def targetVersion = 16
}
