buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

file("../gradle.properties").withInputStream {
    def props = new Properties()
    props.load(it)
    props.each { prop -> project.ext.set(prop.key, prop.value) }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

version = mod_version
group = maven_group
archivesBaseName = "${mod_name}-${minecraft_version}-forge"

eclipse {
    project { name = "${mod_name}-${minecraft_version}-forge" }
}

minecraft {
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('../run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                musketmod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('../run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                musketmod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.java { srcDir '../src/main/java' }
sourceSets.main.resources { srcDir '../src/main/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}

jar {
    manifest {
        attributes([
            "Implementation-Version": mod_version,
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
